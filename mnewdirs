#!/usr/bin/env perl
##
# find maildirs with new messages by default spit out lines like this:
#     dir count
# in this form dir will have /new at the end if the -n option is
# specified, we instead spit out:
#     maildir
# which is just dirname $dir

use strict;
use warnings;
use File::Basename;
use File::Find;
use Getopt::Std;

our %newdirs = (); # dir => #files
our %opts;
our $MBLAZE = $ENV{'MBLAZE'} || join('/',$ENV{'HOME'},'.mblaze');

sub mhdr {
	open(MHDR, "mhdr -h $_[0] $MBLAZE/profile |")
		or die "mhdr: $!";
	chomp(my $val = <MHDR>);
	close(MHDR);
	return $val;
}

our $maildir = mhdr("MaildirBase") || $ENV{'MAILDIR'} ||
	join('/',$ENV{'HOME'},'mail');
$maildir = $ENV{"HOME"}."/Maildir" unless -d $maildir;
die "no maildir and no MAILDIR set!?\n" unless -d $maildir;

sub check {
	$File::Find::name =~ /\/new\/\d+\.\d+_\d+.*$/ &&
	    $newdirs{$File::Find::dir}++;
}

getopts('n',\%opts);
our $fmt = $opts{'n'} ? "%s\n" : "%s %d\n";
find(\&check, $maildir);
foreach (sort keys %newdirs) {
	my @out = ($opts{'n'} ? dirname($_) : $_);
	push(@out, $newdirs{$_}) unless $opts{'n'};
	printf($fmt,@out);
}
